plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id("org.jreleaser") version "1.22.0"
    id "com.github.ben-manes.versions" version '0.53.0'
}

group = 'io.github.igrqb'
version = '0.5.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.jnr:jnr-ffi:2.2.18'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.32'

    testImplementation(platform('org.junit:junit-bom:6.0.3'))
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.3'
    testImplementation 'org.junit.platform:junit-platform-suite-engine:6.0.3'
    testImplementation 'commons-io:commons-io:2.21.0'
    testImplementation 'com.twelvemonkeys.imageio:imageio-batik:3.13.0'
    testImplementation 'org.apache.pdfbox:pdfbox:3.0.6'
    testImplementation 'org.apache.xmlgraphics:batik-transcoder:1.19'
    testImplementation 'org.bytedeco:javacv-platform:1.5.12'
    testImplementation 'org.bytedeco:opencv-platform:4.11.0-1.5.12'
}

def isNonStable = { String version ->
    // added -JRE for guava
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', '-JRE'].any { theVersion -> version.toUpperCase().contains(theVersion) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    description 'Displays the dependency updates for the project via https://github.com/ben-manes/gradle-versions-plugin'
    rejectVersionIf {
      isNonStable(it.candidate.version)
    }
}

test {
    useJUnitPlatform()
}



java {
    withSourcesJar()
    withJavadocJar()
}


publishing {
    repositories {
        maven {
            name = 'staging'
            url = layout.buildDirectory.dir('staging-deploy').get().asFile.toURI()
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'jnr-graphviz'
                packaging = 'jar'
                // optionally artifactId can be defined here
                description = 'Java bindings for simple Graphviz commands using JNR'
                url = 'https://github.com/igrqb/jnr-graphviz'

                scm {
                    connection = 'scm:git:https://github.com/igrqb/jnr-graphviz.git'
                    developerConnection = 'scm:git:ssh:git@github.com:igrqb/jnr-graphviz.git'
                    url = 'https://github.com/igrqb/jnr-graphviz'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://mit-license.org/'
                    }
                }

                developers {
                    developer {
                        id = 'igrqb'
                        name = 'igrqb'
                        email = 'igrqb@duck.com'
                    }
                }
            }

            from components.java
        }
    }
}


signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

jreleaser {
    // minimal config needed for Maven Central only
    project {
        // usually autodetected from gradle
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    // autoRelease = true
                    // closeDelay = 'PT5M'
                }
            }
        }
    }

    signing {
        pgp {
            active          = 'ALWAYS'
            publicKey       = '{{env.PGP_PUBLIC_KEY}}'
            secretKey       = '{{env.PGP_SECRET_KEY}}'
            passphrase      = '{{env.PGP_PASSPHRASE}}'
        }
    }
}