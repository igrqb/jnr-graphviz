plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id("io.github.gradle-nexus.publish-plugin") version '1.3.0'
}

group = 'io.github.igrqb'
version = '0.1'

sourceSets {
    main.java.srcDirs 'src'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.jnr:jnr-ffi:2.2.14'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'ch.qos.logback:logback-classic:1.4.7'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'commons-io:commons-io:2.14.0'
    testImplementation 'com.twelvemonkeys.imageio:imageio-batik:3.9.4'
    testImplementation 'org.apache.xmlgraphics:batik-transcoder:1.17'
    testImplementation 'org.bytedeco:javacv:1.5.9'
    testImplementation 'org.bytedeco:javacv-platform:1.5.9'
    testImplementation 'org.bytedeco:opencv:4.7.0-1.5.9'
    testImplementation 'org.bytedeco:opencv-platform-gpu:4.7.0-1.5.9'
}

test {
    useJUnitPlatform()
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}


//tasks.withType(Javadoc) {
//    failOnError false
//    options.addStringOption('Xdoclint:none', '-quiet')
//    options.addStringOption('encoding', 'UTF-8')
//    options.addStringOption('charSet', 'UTF-8')
//}

java {
    withSourcesJar()
    withJavadocJar()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'jnr-graphviz'
                packaging = 'jar'
                // optionally artifactId can be defined here
                description = 'Java bindings for simple Graphviz commands using JNR'
                url = 'https://github.com/igrqb/jnr-graphviz'

                scm {
                    connection = 'scm:git:https://github.com/igrqb/jnr-graphviz.git'
                    developerConnection = 'scm:git:ssh:git@github.com:igrqb/jnr-graphviz.git'
                    url = 'https://github.com/igrqb/jnr-graphviz'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://mit-license.org/'
                    }
                }

                developers {
                    developer {
                        id = 'igrqb'
                        name = 'igrqb'
                        email = 'igrqb@duck.com'
                    }
                }
            }

            from components.java
        }
    }
}

signing {
    def signingKey = project.findProperty("gpgSigningKey")
    def signingPassword = project.findProperty("gpgSigningPass")

    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}